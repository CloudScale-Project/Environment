library csm2pcmData;

modeltype OVERVIEW uses overview('http://org.scaledl.overview/ScaleDLOverviewComponentModel/1.0');
modeltype REPOSITORY uses pcm::repository('http://palladiosimulator.org/PalladioComponentModel/5.0');

configuration property CSMID : String;

mapping OVERVIEW::parametertype::Parameter::parameter2dataType() : REPOSITORY::DataType
	disjuncts OVERVIEW::parametertype::Parameter::parameterPrimitive2dataType,
				OVERVIEW::parametertype::Parameter::parameterComposite2dataType,
				OVERVIEW::parametertype::Parameter::parameterCollection2dataType
	{}

//primitive paramtere transformation
mapping OVERVIEW::parametertype::Parameter::parameterPrimitive2dataType() : PrimitiveDataType
	when{self.oclIsKindOf(OVERVIEW::parametertype::PrimitiveParameter)} {
	
	var par : OVERVIEW::parametertype::PrimitiveParameter;
	par := self.oclAsType(OVERVIEW::parametertype::PrimitiveParameter);
		
	//INT
	if(par.type = OVERVIEW::parametertype::TypeEnum::INT) then{
		type := REPOSITORY::PrimitiveTypeEnum::INT;
	}endif;
	
	//STRING
	if(par.type = OVERVIEW::parametertype::TypeEnum::STRING) then{
		type := REPOSITORY::PrimitiveTypeEnum::STRING;
	}endif;
	
	//BOOL
	if(par.type = OVERVIEW::parametertype::TypeEnum::BOOL) then{
		type := REPOSITORY::PrimitiveTypeEnum::BOOL;
	}endif;
	
	//DOUBLE
	if(par.type = OVERVIEW::parametertype::TypeEnum::DOUBLE) then{
		type := REPOSITORY::PrimitiveTypeEnum::DOUBLE;
	}endif;
}

//composite parameter transformation
mapping OVERVIEW::parametertype::Parameter::parameterComposite2dataType() : CompositeDataType 
	when{self.oclIsKindOf(OVERVIEW::parametertype::CompositeParameter)}{
	
	var par : OVERVIEW::parametertype::CompositeParameter;
	par := self.oclAsType(OVERVIEW::parametertype::CompositeParameter);
	
	entityName := self.name;
	parentType_CompositeDataType := par._extends.late resolveoneIn(OVERVIEW::parametertype::Parameter::parameterComposite2dataType,
																   REPOSITORY::CompositeDataType);
	innerDeclaration_CompositeDataType := par.parameters->map parameter2InnerDeclaration(result);
}

//collection parameter trensformation
mapping OVERVIEW::parametertype::Parameter::parameterCollection2dataType() : CollectionDataType 
	when{self.oclIsKindOf(OVERVIEW::parametertype::CollectionParameter)}{
	
	var par : OVERVIEW::parametertype::CollectionParameter;
	par := self.oclAsType(OVERVIEW::parametertype::CollectionParameter);
	
	entityName := self.name;
	innerType_CollectionDataType := par.parameter.late resolveoneIn(OVERVIEW::parametertype::Parameter::parameter2dataType, 
																	REPOSITORY::CollectionDataType);
}

mapping OVERVIEW::parametertype::Parameter::parameter2InnerDeclaration(in parent : CompositeDataType) : InnerDeclaration{
	
	entityName := self.name;
	compositeDataType_InnerDeclaration := parent;
	datatype_InnerDeclaration := self.late resolveoneIn(OVERVIEW::parametertype::Parameter::parameter2dataType, 
														REPOSITORY::DataType)
}
	
	